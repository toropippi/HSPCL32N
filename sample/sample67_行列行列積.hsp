//N*N要素の行列AとBをかけてCを求める計算
//row major
#include "hspcl32n.as"
	clinit
	randomize
	
	//配列 A は m x k 行列、配列 B は k x n 行列、配列 C は m x n 行列
	n=rnd(1024*8)+1
	k=rnd(1024*8)+1
	m=rnd(1024*8)+1
	mes "n=" + n + " m=" + m + " k=" + k + ""
	
	//カーネル設定
	prgid=clCreateProgram("SGEMM.cl")
	krnSGEMM_a=clCreateKernel(prgid,"SGEMM_a")
	krnSGEMM_k=clCreateKernel(prgid,"SGEMM_k")
	krnSGEMM_s=clCreateKernel(prgid,"SGEMM_small")
	krnRnd=clCreateKernel(prgid,"GenerateRandom")
	
	//行列初期化
	gpu_A = clCreateBuffer(m*k*4)
	gpu_B = clCreateBuffer(k*n*4)
	gpu_C = clCreateBuffer(m*n*4)
	//まずは初期値をGPUで生成
	clSetKrns krnRnd,gpu_A,rnd(9999)
	clDoKrn1_sub krnRnd,m*k,64
	clSetKrns krnRnd,gpu_B,rnd(9999)
	clDoKrn1_sub krnRnd,k*n,64
	
	// GPUで計算
	mes "計算開始"
	gosub*mySGEMM
	clFinish
	mes "計算終了"
	gosub*検算
	stop



//mySGEMM(m, n, k, gpu_A, gpu_B, gpu_C);
*mySGEMM
	if ((n < 128) | (m < 128)){
	    kernel = krnSGEMM_s
	}else{
	    if (k \ 16 == 0){
	        kernel = krnSGEMM_k
	    }else{
	        kernel = krnSGEMM_a
	    }
	}
	
	//引数をセット
	clSetKrns kernel,m,n,k,gpu_A,gpu_B,gpu_C
	
	//計算
	clDokrn2 kernel,(n + 127) / 128 * 16, (m + 127) / 128 * 16,16,16
	return


*検算
	cldim_fpFromBuffer cpu_A,gpu_A
	cldim_fpFromBuffer cpu_B,gpu_B
	cldim_fpFromBuffer cpu_C,gpu_C

	colno=rnd(n)
	rowno=rnd(m)

	mes
	mes "行="+rowno+" 列="+colno+" の結果"
	gpu_ans=cpu_C.(rowno*n+colno)
	mes "gpu_C["+rowno+"]["+colno+"]="+gpu_ans+""
	
	cpu_ans=clfloat(0.0)
		repeat k
		a=cpu_A.(rowno*k+cnt)
		b=cpu_B.(cnt*n+colno)
		cpu_ans+=a*b
		loop
	

	len=strlen(str(rowno))+strlen(str(colno))
	sp=""
	repeat len:sp+=" ":loop

	mes "CPU検算  "+sp+"="+cpu_ans+""
	return